dependencies {
    compile project(":etm-launcher")
}

apply plugin: 'application'

mainClassName = 'com.jecstar.etm.launcher.Startup'
applicationName = 'etm'

distributions {
    main {
    	baseName 'etm'
        contents {
            exclude '**/com.ibm.mq.allclient.jar'
            exclude '**/config-manager-proxy-S900-FP03.jar'
            exclude '**/ibmjsseprovider2.jar'
            exclude '**/integration-api-10.0.0.0.jar'
            into ('config') { 
            	from { 'src/main/distribution/config' }
            }
        }
    }
}

tasks.withType(Tar) { task ->
    compression = Compression.GZIP
    task.doLast {
        ant.checksum(file: task.archivePath, algorithm: 'SHA-512')
    }
}

tasks.withType(Zip) { task ->
    task.doLast {
        ant.checksum(file: task.archivePath, algorithm: 'SHA-512')
    }
}

startScripts {
    doLast {
        unixScript.text = file('src/main/distribution/scripts/etm.sh').text
            .replace('${applicationName}', applicationName)
            .replace('${applicationVersion}', version)
            .replace('${mainClassName}', mainClassName)
        windowsScript.text = file('src/main/distribution/scripts/etm.bat').text
            .replace('APP_HOME=%DIRNAME%..', 'APP_HOME=%DIRNAME%')
            .replace('%APP_HOME%\\lib', '%APP_HOME%\\wrapper')
            .replace('%GAIDENW_OPTS%', '%GAIDENW_OPTS% -Dapp.home=%APP_HOME%')
    }
}