apply plugin: 'application'

dependencies {
    compile project(":etm-launcher")
}


mainClassName = 'com.jecstar.etm.launcher.Startup'
applicationName = 'etm'

distributions {
    main {
    	baseName 'etm'
        contents {
            exclude '**/com.ibm.mq.allclient.jar'
            exclude '**/config-manager-proxy-S900-FP03.jar'
            exclude '**/ibmjsseprovider2.jar'
            exclude '**/integration-api-*.jar'
            exclude '**/etm-distribution-*.jar'
            into ('config') { 
            	from { 'src/main/distribution/config' }
            }
            into ('bin') { 
            	from ('src/main/distribution/scripts') {
            		include 'etm.service'
            	}
            }            
        }
    }
}

tasks.withType(Tar) { task ->
    compression = Compression.GZIP
    task.doLast {
        ant.checksum(file: task.archivePath, algorithm: 'SHA-512', fileext: '.sha512', pattern: "{0} {1}")
    }
}

tasks.withType(Zip) { task ->
    task.doLast {
        ant.checksum(file: task.archivePath, algorithm: 'SHA-512', fileext: '.sha512', pattern: "{0} {1}")
    }
}

startScripts {
    doLast {
        unixScript.text = file('src/main/distribution/scripts/etm.sh').text
            .replace('${applicationName}', applicationName)
            .replace('${applicationVersion}', version)
            .replace('${mainClassName}', mainClassName)
        windowsScript.text = file('src/main/distribution/scripts/etm.bat').text
            .replace('${applicationName}', applicationName)
            .replace('${applicationVersion}', version)
            .replace('${mainClassName}', mainClassName)
    }
}