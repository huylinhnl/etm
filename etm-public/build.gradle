ext {
    version_elasticsearch = "7.0.0"
    version_junit = "5.4.0"
    dep_jsonPath = "com.jayway.jsonpath:json-path:2.4.0"
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    version "4.0.0"
    group "com.jecstar.etm"

    repositories {
        mavenCentral()
    }

    plugins.withType(JavaPlugin).whenPluginAdded {
        compileJava {
            sourceCompatibility = '11'
            targetCompatibility = '11'
            options.encoding = 'UTF-8'
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }

        compileTestJava {
            sourceCompatibility = '11'
            targetCompatibility = '11'
            options.encoding = 'UTF-8'
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }


        dependencies {
            testImplementation 'org.junit.jupiter:junit-jupiter-api:' + version_junit
            testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:' + version_junit
            testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:' + version_junit
        }

        sourceSets.whenObjectAdded {
            if ('integrationTest'.equals(it.name)) {
                dependencies {
                    integrationTestImplementation 'org.junit.jupiter:junit-jupiter-api:' + version_junit
                    integrationTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:' + version_junit
                    integrationTestRuntimeOnly 'org.junit.vintage:junit-vintage-engine:' + version_junit
                }
            }
        }

        test {
            useJUnitPlatform()
        }
    }
}
